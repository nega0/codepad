#!/usr/bin/perl -w

## public domain

use HTTP::Request::Common;
use LWP::UserAgent;
use File::Basename;
use Getopt::Std;
use strict;

## parse options
our ($opt_l, $opt_p, $opt_n, $opt_r);
getopts('l:npr');

## default values
my %file_types = ('c'      => 'C',
                  'cc'     => 'C++',
                  'cxx'    => 'C++',
                  'cpp'    => 'C++',
                  'd'      => 'D',
                  'hs'     => 'Haskell',
                  'lua'    => 'Lua',
                  'ml'     => 'OCaml',
                  'php'    => 'PHP',
                  'pl'     => 'Perl',
                  'perl'   => 'Perl',
                  'txt'    => 'Plain Text',
                  'py'     => 'Python',
                  'python' => 'Python',
                  'rb'     => 'Ruby',
                  'ruby'   => 'Ruby',
                  'scm'    => 'Scheme',
                  'tcl'    => 'Tcl',
                  'tclsh'  => 'Tcl',);
my $private = !$opt_p ? "True" : "False";
my $lang    = !$opt_l ? 'Plain Text' : ${file_types{$opt_l}};
my $run     =  $opt_r ? "True" : "False";

## no user serviceable parts inside

## abort early on a bad lanuguage
if ($opt_l) {
  if (not exists ${file_types{$opt_l}}) {
    die "Unsupported language";
  }
}

## options/filenames after getopt did its parsing
if ($#ARGV > 0) {
  die "Be nice to codepad.org. Paste one file at a time.\n"
}

## conditionally look for a clipboard
my $clip = 1;
if (($^O ne "darwin") and ($^O ne "MSWin32")) {
  ## assume unix
  if (exists($ENV{"DISPLAY"})) {
    eval "use Clipboard;";
    if ($@) {
      ## loading clipboard failed
      $clip = 0;
    }
  } else {
    ## cant find X11 on unix
    $clip = 0;
  }
}

## file extension handling
my $no_ext = 1;  ## true, don't know filetype by its extension
if (!$opt_l and $ARGV[0]) {
  my ($b, $d, $e) = fileparse($ARGV[0], qr/[^.]+$/);
  if ($e and exists ${file_types{$e}}) {
    $lang = ${file_types{$e}};
    $no_ext = 0;  ## yay! i figured it out
  }
}

## construct data to feed to our request
my $code    = [ do { local $\; <>; } ];
my $data    = [ 'code' => "@$code",
                'lang' => $lang,
                'run'  => $run,
                'private' => $private,
                'submit' => "Submit",
              ];

## sh-bang handling
if (!$opt_l and $no_ext) {
  my $sb = substr($code->[0], 0, -1);
  if ($sb =~ /^#![^\s]+\/(?:env\s+)?([^\s]+)/) {
    my $e = $1;
    if ($e and exists ${file_types{$e}}) {
      $lang = ${file_types{$e}};
    }
  }
}

my $req = POST "http://codepad.org/", $data;
### req: $req->as_string;

unless ($opt_n) {
  my $ua = LWP::UserAgent->new;
  my $r = $ua->request($req);
  ### r: $r->as_string;
  if ($clip) {
    Clipboard->copy($r->header("Location"));
  } else {
    print $r->header("Location") . "\n";
  }
}
